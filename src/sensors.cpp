#include<Arduino.h>
#include<sensors.h>
#include<pins.h>
#include <IRremote.hpp>
#include <ESP32Servo.h>
#include "DHT.h"
#include <PZEM004Tv30.h>
#include "tft_display.h"

//================================================
//Bi·∫øn to√†n c·ª•c 
//================================================
volatile SensorData sensordata; //D·ªØ li·ªáu c·∫£m bi·∫øn chung
Servo myServo;
DHT dht(DHTPIN, DHTTYPE);
PZEM004Tv30 pzem(Serial2, PZEM_RX, PZEM_TX);

volatile bool alarmEnabled = true;         //ch·ªëng tr·ªôm ON/OFF
bool motionState = false;                  //Tr·∫°ng th√°i ƒëang b√°o ƒë·ªông PIR
unsigned long lastMotionTime = 0;
const unsigned long alarmDuration = 3000;  //Th·ªùi gian c√≤i b√°o ƒë·ªông (ms)

volatile bool rainSensorEnabled = true;    //C·∫£m bi·∫øn m∆∞a ON/OFF
volatile bool isRaining = false;

unsigned long previousServoMillis = 0;    
const unsigned long servoInterval = 15;  // th·ªùi gian c·∫≠p nh·∫≠t m·ªói b∆∞·ªõc (ms)
float servoCurrent = 0;                  // g√≥c hi·ªán t·∫°i
volatile float servoTarget = 0;          // g√≥c ƒë√≠ch
float servoSpeed = 1;                    // t·ªëc ƒë·ªô (ƒë·ªô m·ªói b∆∞·ªõc)

const unsigned long blinkDelayTime = 150;
//==================================================
//C·ªù c·∫£nh b√°o s·ª± ki·ªán (d√πng ƒë·ªÉ g·ª≠i MQTT)
//=================================================
volatile bool coTrom = false;
volatile bool batDauMua = false;
volatile bool hetMua = false;
volatile bool matDien = false;
volatile bool coDien = false;
//=====================================================
// ƒê·ªçc PZEM-004T (ƒêi·ªán √°p , d√≤ng, c√¥ng su·∫•t,...)
//=====================================================
void readPZEM() {
  static unsigned long lastReadPZEM = 0;
  const unsigned long readInterval = 1000;
  if (millis() - lastReadPZEM < readInterval) return;
  lastReadPZEM = millis();

  sensordata.voltage = pzem.voltage();
  sensordata.current = pzem.current();
  sensordata.power   = pzem.power();
  sensordata.energy  = pzem.energy();
  sensordata.frequency = pzem.frequency();
  sensordata.pf      = pzem.pf();

  static float dienApCu = 220.0;
  //Ph√°t hi·ªán m·∫•t ƒëi·ªán
  if (isnan(sensordata.voltage) || sensordata.voltage < 50.0) {
    if (dienApCu >= 50.0) {
        matDien = true;
        coDien  = false;
        Serial.println("‚ö° M·∫§T ƒêI·ªÜN ho·∫∑c L·ªñI ƒê·ªåC PZEM!");
    }
    dienApCu = 0; // c·∫≠p nh·∫≠t ƒëi·ªán √°p c≈©
    return;
  }
  // Ph√°t hi·ªán ƒëi·ªán c√≥ l·∫°i
  if (sensordata.voltage >= 100.0 && dienApCu < 50.0) {
      coDien  = true;
      matDien = false;
      Serial.println("‚ö° ƒê√É C√ì ƒêI·ªÜN!");
  }
    dienApCu = sensordata.voltage;

  Serial.printf("‚ö° V: %.1fV | A: %.2fA | W: %.1fW | kWh: %.3f | Hz: %.1f | PF: %.2f\n",
                sensordata.voltage, sensordata.current, sensordata.power, sensordata.energy, sensordata.frequency, sensordata.pf);
  displayPZEM(sensordata.energy, sensordata.power);
}
//=================================================
// ƒê·ªçc DHT22 (nhi·ªát ƒë·ªô v√† ƒë·ªô ·∫©m)
//=================================================
void readDHT22() {
  static unsigned long lastReadTime = 0;
  const unsigned long readInterval = 5000; // 5 s ƒë·ªçc 1 l·∫ßn
  if (millis() - lastReadTime < readInterval) return;
  lastReadTime = millis();

  sensordata.humidity = dht.readHumidity();
  sensordata.temperature = dht.readTemperature();
  if (isnan(sensordata.humidity) || isnan(sensordata.temperature)) {
    Serial.println("‚ùå L·ªói ƒë·ªçc c·∫£m bi·∫øn DHT22!");
    return;
  }

  Serial.printf("üíß %.1f%%  üå°Ô∏è %.1f¬∞C\n", sensordata.humidity, sensordata.temperature);
  displayDHT(sensordata.temperature, sensordata.humidity);
}
//
// C·∫≠p nh·∫≠t c·∫£m bi·∫øn m∆∞a-> t·ª± ƒë·ªông r√∫t/ph∆°i qu·∫ßn √°o
void updateRain() {
  static unsigned long lastPrint = 0;
  if (!rainSensorEnabled) return;

  int rainValue = analogRead(WATER_PIN);
  const int threshold = 500;     //Ng∆∞·ª°ng ƒëi·ªÅu ch·ªânh t√πy c·∫£m bi·∫øn 
  bool rainingNow = (rainValue > threshold);

  if (rainingNow != isRaining) {
    isRaining = rainingNow;
    if (isRaining) {
      Serial.println("üåßÔ∏è Tr·ªùi m∆∞a, r√∫t qu·∫ßn √°o");
      servoTarget = 90;
      batDauMua = true; // C·ªù theo d√µi th·ªùi ti·∫øt
    } else {
      Serial.println("‚òÄÔ∏è Tr·ªùi kh√¥, ph∆°i qu·∫ßn √°o");
      servoTarget = 0;
      hetMua = true; //C·ªù theo d√µi th·ªùi ti·∫øt
    }
  }

  if (millis() - lastPrint > 5000) {
    Serial.printf("Rain value: %d\n", rainValue);
    lastPrint = millis();
  }
}
//==============================================
// Di chuy·ªÉn SERVO m∆∞·ª£t 
//==============================================
void updateServo() {
  unsigned long currentMillis = millis();

  // ch·ªâ c·∫≠p nh·∫≠t servo m·ªói "servoInterval" mili-gi√¢y
  if (currentMillis - previousServoMillis >= servoInterval) {
    previousServoMillis = currentMillis;

    if (servoCurrent < servoTarget) {
      servoCurrent += servoSpeed;
      if (servoCurrent > servoTarget) servoCurrent = servoTarget;  // tr√°nh v∆∞·ª£t qu√°
      myServo.write(servoCurrent);
    } 
    else if (servoCurrent > servoTarget) {
      servoCurrent -= servoSpeed;
      if (servoCurrent < servoTarget) servoCurrent = servoTarget;
      myServo.write(servoCurrent);
    }
  }
}
//=======================
// Nh√°y LED 
//=======================
struct BlinkState {
  int ledPin = 0;           // Ch√¢n LED
  int times = 0;            // S·ªë l·∫ßn nh√°y (b·∫≠t/t·∫Øt)
  int currentCount = 0;     // ƒê·∫øm s·ªë l·∫ßn ƒë√£ nh√°y
  bool isActive = false;    // Tr·∫°ng th√°i nh√°y (ƒëang ho·∫°t ƒë·ªông hay kh√¥ng)
  unsigned long lastToggle = 0; // Th·ªùi ƒëi·ªÉm chuy·ªÉn ƒë·ªïi cu·ªëi
  unsigned long delayTime = blinkDelayTime; // Th·ªùi gian ch·ªù m·ªói l·∫ßn nh√°y
};
BlinkState blinkState;      // Bi·∫øn l∆∞u tr·∫°ng th√°i nh√°y LED

//=================BLINK LED=======================
// Kh·ªüi t·∫°o nh√°y LED 
void blinkLED(int ledPin, int times, unsigned long delayTime = blinkDelayTime) {
  if (blinkState.isActive) return; // Tho√°t n·∫øu ƒëang nh√°y ƒë·ªÉ tr√°nh xung ƒë·ªôt
  
  // Kh·ªüi t·∫°o tr·∫°ng th√°i nh√°y
  blinkState.ledPin = ledPin;
  blinkState.times = times * 2; // M·ªói nh√°y g·ªìm b·∫≠t v√† t·∫Øt
  blinkState.currentCount = 0;
  blinkState.isActive = true;
  blinkState.lastToggle = millis();
  blinkState.delayTime = delayTime;
  
  digitalWrite(ledPin, !digitalRead(ledPin)); // ƒê·∫£o tr·∫°ng th√°i LED ban ƒë·∫ßu
}
// C·∫≠p nh·∫≠t tr·∫°ng th√°i nh√°y LED trong v√≤ng l·∫∑p ch√≠nh
void updateBlinkLED() {
  if (!blinkState.isActive) return; // Tho√°t n·∫øu kh√¥ng nh√°y
  
  unsigned long currentMillis = millis();
  if (currentMillis - blinkState.lastToggle >= blinkState.delayTime) {
    // ƒê·∫£o tr·∫°ng th√°i LED
    digitalWrite(blinkState.ledPin, !digitalRead(blinkState.ledPin));
    blinkState.currentCount++;
    blinkState.lastToggle = currentMillis;
    
    // K·∫øt th√∫c nh√°y khi ƒë·ªß s·ªë l·∫ßn
    if (blinkState.currentCount >= blinkState.times) {
      blinkState.isActive = false;
    }
  }
}
//===============================================
// Nh·∫≠n l·ªánh t·ª´ IR receiver h·ªìng ngo·∫°i
//===============================================
void checkIR() {
  if (IrReceiver.decode()) {
    unsigned long code = IrReceiver.decodedIRData.decodedRawData;
    Serial.printf("üì• M√£ nh·∫≠n ƒë∆∞·ª£c: 0x%lX\n", code);

    switch (code)
    {
    case 0xF30CFF00: //Toggle servo
        servoTarget = (servoTarget == 0) ? 90 : 0;
        Serial.println("Remote: Toggle servo");
        break;
    case 0xE718FF00: // Toggle rain sensor
        rainSensorEnabled = !rainSensorEnabled;
        Serial.printf("Remote: C·∫£m bi·∫øn m∆∞a %s\n", rainSensorEnabled ? "B·∫¨T" : "T·∫ÆT");
        break;
    case 0xE916FF00://Toggle PIR
        alarmEnabled = !alarmEnabled;
        Serial.printf("Remote: PIR %s\n", alarmEnabled ? "B·∫¨T" : "T·∫ÆT");
        if (alarmEnabled) {
        blinkLED(LED_GREEN, 2);
        digitalWrite(LED_GREEN, HIGH);
        digitalWrite(LED_RED, LOW);
        } else {
        blinkLED(LED_RED, 2);
        digitalWrite(LED_GREEN, LOW);
        digitalWrite(LED_RED, LOW);
        digitalWrite(BUZZER, HIGH);
        motionState = false;
        }
        break;

    default:
       Serial.printf("M√£ remote kh√¥ng x·ª≠ l√Ω : 0x%lX\n", code);
       break;
    }
    IrReceiver.resume();// s·∫µn s√†ng nh·∫≠n l·ªánh ti·∫øp theo
  }
}
//=====================================
// X·ª≠ l√Ω c·∫£m bi·∫øn chuy·ªÉn ƒë·ªông PIR (ch·ªëng tr·ªôm)
//=====================================
void updatePIR() {
  if (!alarmEnabled) return;

  bool motionDetected = digitalRead(PIR_PIN);
  if (motionDetected && !motionState) {
    motionState = true;
    lastMotionTime = millis();
    Serial.println("‚ö†Ô∏è Ph√°t hi·ªán chuy·ªÉn ƒë·ªông!");
    digitalWrite(LED_RED, HIGH);
    digitalWrite(BUZZER, LOW);
    digitalWrite(LED_GREEN, LOW);
    coTrom = true;
  }

  if (motionState && millis() - lastMotionTime > alarmDuration) {
    motionState = false;
    digitalWrite(LED_RED, LOW);
    digitalWrite(LED_GREEN, HIGH);
    digitalWrite(BUZZER, HIGH);
  }
}
//============================================
// C·∫≠p nh·∫≠t m√†n h√¨nh TFT
//============================================
void updateDisplay() {
  displayStatus(isRaining, servoCurrent, rainSensorEnabled);
}

//=====================================
//Kh·ªüi t·∫°o t·∫•t c·∫£ c·∫£m bi·∫øn v√† ngo·∫°i vi
//======================================
void initSensor(){
    Serial2.begin(9600, SERIAL_8N1, PZEM_RX, PZEM_TX);
    dht.begin();
    myServo.attach(13);
    myServo.write(servoCurrent);

    pinMode(PIR_PIN, INPUT);
    pinMode(LED_GREEN, OUTPUT);
    pinMode(LED_RED, OUTPUT);
    pinMode(WATER_PIN, INPUT);
    pinMode(BUZZER, OUTPUT);
    digitalWrite(BUZZER, HIGH); // M·∫∑t ƒë·ªãnh buzzer k k√™u 
    IrReceiver.begin(IR_RECEIVE_PIN, ENABLE_LED_FEEDBACK); //B·∫≠t nh·∫≠n IR

    //Tr·∫°ng th√°i ban ƒë·∫ßu
    alarmEnabled = false;
    digitalWrite(LED_GREEN, LOW);
    digitalWrite(LED_RED, LOW);
}
