#include"wifi_mqtt.h"
#include<ArduinoJson.h>

WiFiClientSecure espClient;   // D√πng k·∫øt n·ªëi b·∫£o m·∫≠t
PubSubClient client(espClient); //D√πng th∆∞ vi·ªán PubSubClient ƒë·ªÉ k·∫øt n·ªëi v·ªõi MQTT qua b·∫£o m·∫≠t tr√™n
//==================================================
//    Callback khi c√≥ tin nh·∫Øn t·ªõi t·ª´ c√°c topic
//==================================================
void mqttCallback(char* topic, byte* payload, unsigned int length){
    Serial.println("üì• Callback ƒë√£ k√≠ch ho·∫°t!");
    //chuy·ªÉn payload th√†nh string
    String msg;
    for(unsigned int i=0; i< length; i++){
        msg += (char)payload[i];
    }
    Serial.printf("üì© Nh·∫≠n t·ª´ topic[%s]: %s\n", topic, msg.c_str());
    //Dieu khien cam bien chong trom
    if(String(topic)=="home/control/pir"){
        if(msg=="ON"){
            Serial.println(msg);
            alarmEnabled= true;
            blinkLED(LED_GREEN, 2, 150);
            digitalWrite(LED_GREEN, HIGH);
            digitalWrite(LED_RED, LOW);
            Serial.println("‚úÖ Ch·ªëng tr·ªôm: B·∫¨T");
        }else if(msg=="OFF"){
            alarmEnabled= false;
            blinkLED(LED_RED, 2, 150);
            digitalWrite(LED_GREEN, LOW);
            digitalWrite(LED_RED, LOW);
            digitalWrite(BUZZER, HIGH);
            Serial.println("‚ùåCh·ªëng tr·ªôm: T·∫ÆT");
        }
    }
    //dieu khien cam bien mua
    else if(String(topic)== "home/control/rain"){
        if(msg=="ON"){
            rainSensorEnabled= true;
            Serial.println("‚úÖ Rain Sensor: B·∫¨T");
        }
        else if(msg=="OFF"){
            rainSensorEnabled= false;
            Serial.println("‚ùåRain Sensor: T·∫ÆT");
        }
    }
    else if(String(topic)== "home/control/clothes"){
        if(msg=="ON"){
            servoTarget=0; // ph∆°i qu·∫ßn √°o
            Serial.println("üëï Ph∆°i qu·∫ßn √°o");
        }
        else if(msg=="OFF"){
            servoTarget=90; // rut quan ao
            Serial.println("üëï R√∫t qu·∫ßn √°o");
        }
    }
}
//=======================================
//   G∆∞i d≈© li·ªáu l√™n MQTT( g·ª≠i ƒë·ªãnh k·ª≥)
//=======================================
void publishSensorData(){
    StaticJsonDocument<256> doc;
    doc["temperature"] = sensordata.temperature;
    doc["humidity"] = sensordata.humidity;
    doc["voltage"] = sensordata.voltage;
    doc["current"] = sensordata.current;
    doc["power"] = sensordata.power;
    doc["energy"] = sensordata.energy;
    doc["frequency"] = sensordata.frequency;
    doc["pf"] = sensordata.pf;
    //gui mqtt
    char buffer[256];
    serializeJson(doc, buffer); //Bi·∫øn doc th√†nh d·∫°ng chu·ªói json
    Serial.println("JSON g·ª≠i ƒëi: ");
    Serial.println(buffer);
    client.publish("home/sensors/data", buffer);
}

// Ki·ªÉm tra tr·∫°ng th√°i th·ª±c t·∫ø c·ªßa thi·∫øt b·ªã (ch·ªâ g·ª≠i khi c√≥ s·ª± thay ƒë·ªïi)
bool firstRun= true;
bool lastAlarmEnabled = false;
bool lastRainSensorEnabled = false;
float lastServoTarget = -1; // 0 ‚Üí ON, 90 ‚Üí OFF
void publishState() {
    if(firstRun || 
        alarmEnabled != lastAlarmEnabled || 
        rainSensorEnabled != lastRainSensorEnabled || 
        servoTarget != lastServoTarget) {
        //Tao json
        StaticJsonDocument<256> doc;
        doc["pir"] = alarmEnabled ? "ON" : "OFF";
        doc["watersensor"] = rainSensorEnabled ? "ON" : "OFF";
        doc["clothes"] = (servoTarget == 0) ? "ON" : "OFF";
        char buffer[256];
        serializeJson(doc, buffer);
        client.publish("home/device/state", buffer);
        //debug 
        Serial.println("üì§ Tr·∫°ng th√°i thi·∫øt b·ªã g·ª≠i ƒëi:");
        serializeJsonPretty(doc, Serial);
        Serial.println();
        // c·∫≠p nh·∫≠t bi·∫øn l∆∞u tr·∫°ng th√°i
        lastAlarmEnabled = alarmEnabled;
        lastRainSensorEnabled = rainSensorEnabled;
        lastServoTarget = servoTarget;
        firstRun = false;//ƒê√£ g·ª≠i l·∫ßn ƒë√¢u( kh·ªüi ƒë·ªông thi·∫øt b·ªã)
    }
}
//============================================
//  G·ª≠i c√°c c·∫£nh b√°o(tr·ªôm, m·∫•t ƒëi·ªán, m∆∞a)
//============================================
void guiCanhBao() {
  if (!client.connected()) return;

  if (coTrom) {
    client.publish("home/alert/pir", "INTRUDER DETECTED");
    Serial.println("ALERT: [home/alert/pir] INTRUDER DETECTED");
    coTrom = false;
  }
  if (matDien) {
    client.publish("home/alert/power", "POWER OUTAGE");
    Serial.println("ALERT: [home/alert/power] POWER OUTAGE");
    matDien = false;
  }
  if (coDien) {
    client.publish("home/alert/power", "POWER RESTORED");
    Serial.println("ALERT: [home/alert/power] POWER RESTORED");
    coDien = false;
  }
  if (batDauMua) {
    client.publish("home/alert/rain", "RAIN STARTED");
    Serial.println("ALERT: [home/alert/rain] RAIN STARTED");
    batDauMua = false;
  }
  if (hetMua) {
    client.publish("home/alert/rain", "RAIN STOPPED");
    Serial.println("ALERT: [home/alert/rain] RAIN STOPPED");
    hetMua = false;
  }
}

//=========================================================
//              K·∫øt n·ªëi WiFi v√† MQTT
//=========================================================
void connectWiFi(const char* ssid, const char* password){
    WiFi.begin(ssid, password);
    Serial.print("üîå ƒêang k·∫øt n·ªëi WiFi");
    while(WiFi.status() != WL_CONNECTED){
        delay(500);
        Serial.print(".");
    }
    Serial.printf("\n‚úÖ WiFi OK! IP: %s\n", WiFi.localIP().toString().c_str());
}


void connectMQTT(const char* broker, int port, const char* user, const char* pass){
    espClient.setInsecure();
    client.setServer(broker, port);
    client.setCallback(mqttCallback);
    Serial.print("Connecting MQTT...");
    while (!client.connected()){
        if(client.connect("ESP32Client", user, pass)){
            Serial.printf("\n‚úÖ MQTT connected!");
            //dong kiem tra 
            client.subscribe("home/control/#");
            Serial.println("\nüì° ƒê√£ ƒëƒÉng k√Ω topic home/control/#");
        }
        else{
            Serial.print(".");
            delay(1000);
        }
    }
}

//============================================================
//Ph·∫£i g·ªçi li√™n t·ª•c trong loop() ƒë·ªÉ x·ª≠ l√Ω tin nh·∫Øn t·ª´ MQTT
//============================================================
void mqttLoop(){
    client.loop();// duy tr√¨ k·∫øt n·ªëi v·ªõi MQTT
}
